plugins {
    id 'com.sedmelluq.jdaction' version '1.0.2'
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
}

group 'org.samoxive'
version '3.1.1'

apply plugin: 'application'
apply plugin: 'java'
mainClassName = "org.samoxive.safetyjim.MainKt"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven {
        url  "https://dl.bintray.com/kotlin/exposed"
    }
}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

dependencies {
    ktlint 'com.github.shyiko:ktlint:0.27.0'
    testCompile 'junit:junit:4.12'
    compile 'org.slf4j:slf4j-log4j12:1.8.0-beta2'
    compile 'org.postgresql:postgresql:42.2.5'
    compile 'com.zaxxer:HikariCP:3.2.0'
    compile 'com.joestelmach:natty:0.13'
    compile 'org.ocpsoft.prettytime:prettytime:4.0.2.Final'
    compile 'com.mashape.unirest:unirest-java:1.4.9'
    compile 'io.vertx:vertx-web:3.5.3'
    compile 'com.auth0:java-jwt:3.4.0'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'log4j:log4j:1.2.17'
    compile ('net.dv8tion:JDA:3.7.1_405') {
        exclude module: 'opus-java'
    }
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.61'
    compile 'com.uchuhimo:konf:0.11'
    compile 'org.jetbrains.exposed:exposed:0.10.4'
}

jar {
    manifest {
        attributes 'Main-Class': 'org.samoxive.safetyjim.MainKt'
    }
}

shadowJar {
    archiveName = "safetyjim.jar"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}