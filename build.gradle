import com.moandjiezana.toml.Toml

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.moandjiezana.toml:toml4j:0.7.2'
    }
}

plugins {
    id 'nu.studer.jooq' version '2.0.11'
    id 'com.sedmelluq.jdaction' version '1.0.2'
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id 'com.github.ben-manes.versions' version '0.20.0'
}

group 'org.samoxive'
version '3.1.1'

apply plugin: 'java'
apply plugin: 'application'
mainClassName = "org.samoxive.safetyjim.Main"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.8.0-beta2'
    jooqRuntime 'org.postgresql:postgresql:42.2.2'
    compile 'org.postgresql:postgresql:42.2.2'
    compile 'org.jooq:jooq:3.10.0'
    compile 'com.moandjiezana.toml:toml4j:0.7.2'
    compile 'net.dv8tion:JDA:3.7.1_399'
    compile 'com.zaxxer:HikariCP:3.2.0'
    compile "com.joestelmach:natty:0.13"
    compile 'org.ocpsoft.prettytime:prettytime:4.0.2.Final'
    compile 'com.squareup.okhttp3:okhttp:3.11.0'
    compile "io.vertx:vertx-web:3.5.3"
    compile 'com.auth0:java-jwt:3.4.0'
    compile 'com.google.code.gson:gson:2.8.5'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
}

jooq {
    def toml = new Toml().read(new File('./config.toml').text)
    def host = toml.getString('database.host')
    def username = toml.getString('database.user')
    def pass = toml.getString('database.pass')
    def port = toml.getLong('database.port')
    def db = toml.getString('database.name')

    version = '3.9.5'
    edition = 'OSS'
    sample(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = 'jdbc:postgresql://' + host + ':' + port + '/' + db
            user = username
            password = pass
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            target {
                packageName = 'org.samoxive.jooq.generated'
            }
            database {
                name = 'org.jooq.util.postgres.PostgresDatabase'
                inputSchema = 'public'
            }
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'org.samoxive.safetyjim.Main'
    }
}

shadowJar {
    archiveName = "safetyjim.jar"
}